apply plugin: 'com.android.application'

group = 'net.timelegend.chaka'
version = '1.24.9a.14'

dependencies {
	if (file('../lib/build.gradle').isFile())
		api project(':lib')
	else
		api 'net.timelegend.chaka:viewer:1.24.9a.14'
}

android {
	compileSdkVersion 34
	buildToolsVersion '34.0.0'
	defaultConfig {
        applicationId 'net.timelegend.chaka.viewer.app'
        namespace 'net.timelegend.chaka.viewer.app'
		minSdkVersion 21
		targetSdkVersion 34
		versionName '1.24.9a.14'
		versionCode 160

        if (!project.hasProperty('noSplits')) {
	        splits {
		        abi {
			        enable true
			        reset()
			        include('arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64')
			        universalApk true
		        }
	        }
        }
        else if (project.hasProperty('ABI_FILTERS')) {
            ndk {
                abiFilters = []
                abiFilters.addAll(ABI_FILTERS.split(',').collect{it as String})
            }
        }
	}
    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles.
        includeInBundle = false
    }
	if (project.hasProperty('release_storeFile')) {
		signingConfigs {
			release {
				enableV3Signing true
				enableV4Signing true
				storeFile file(release_storeFile)
				storePassword release_storePassword
				keyAlias release_keyAlias
				keyPassword release_keyPassword
			}
		}
		// ndkVersion "25.1.8937393"
		buildTypes {
			release {
				signingConfig signingConfigs.release
				ndk {
					debugSymbolLevel 'FULL'
				}
			}
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}
